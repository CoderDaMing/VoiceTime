apply plugin: 'com.android.application'
apply from: 'and_res_guard.gradle'
// 依赖插件脚本
apply from: 'tinker-support.gradle'
android {
    signingConfigs {
        release {
            keyAlias 'VoiceTime'
            keyPassword '19921215'
            storeFile file('/Users/sunday/Documents/VoiceTime')
            storePassword '19921215'
        }
    }

    //渠道配置，通过产品微调的功能来配置
    productFlavors {
        //添加一个产品配置项，名称是VoiceTime
        CHANNEL_FIR {
            flavorDimensions "3"
            //表示清单文件中有一个占位符channel，编译的时候会将它的值替换
            manifestPlaceholders = [channel:"fir"]
        }

        CHANNEL_360 {
            flavorDimensions "3"
            manifestPlaceholders = [channel:"360"]
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.ming.voicetime"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 4
        versionName "1.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            //APP的build.gradle设置支持的SO库架构
            abiFilters 'armeabi'
        }
        //突破65536方法数限制
        multiDexEnabled true
    }
    buildTypes {
        release {
            //是否优化zip
            zipAlignEnabled true
            // 是否去除无效的资源文件
            shrinkResources true
            //突破65536方法数限制
            multiDexEnabled true
            //混淆配置时改为true,proguard-rules.pro配置相关
            minifyEnabled true
            //混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release
        }
    }

    // 自定义输出配置
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "voicetime_${variant.versionCode}-${variant.versionName}-${releaseTime()}.apk"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

//获取编译时间
static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    //Android的分包库
    implementation 'com.android.support:multidex:1.0.3'
    //腾讯 bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.0'//其中latest.release指代最新版本号，也可以指定明确的版本号，例如1.2.0
    // 指定tinker依赖版本（注：应用升级1.3.5版本起，不再内置tinker）
    implementation 'com.tencent.tinker:tinker-android-lib:1.9.9'
}
